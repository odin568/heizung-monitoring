plugins {
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'idea'
}

// Use LTS version
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
        vendor = JvmVendorSpec.ADOPTOPENJDK
    }
}

// GitHub-Version >> Docker Version >> Fallback
if (System.getenv("RELEASE_VERSION")) {
	version = System.getenv("RELEASE_VERSION")
}
else if (project.hasProperty("RELEASE_VERSION") && project.getProperty("RELEASE_VERSION") && !project.getProperty("RELEASE_VERSION").allWhitespace) {
	version = project.getProperty("RELEASE_VERSION")
}
else {
	version = "SNAPSHOT"
}
println("Building version $version")

// Omit version number in artifacts for simpler releases
bootJar.archiveFileName = "heizung-monitoring.jar"

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'

	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

springBoot {
	buildInfo()
}


task packageDistribution(type: Zip) {
	archiveFileName = "heizung-monitoring.zip"
	destinationDirectory = file("$buildDir/release")
	from "$buildDir/dist"
}

// Adapt docker-compose.yml with correct version
task adaptDockerCompose(type: Copy) {
	from "$buildDir/../deploy/docker-compose.yml"
	into "$buildDir/dist"
	filter { line -> line.replaceAll('latest', version) }
}
task copyArtifacts {
	mkdir "$buildDir/dist/grafana"
	copy {
		from "$buildDir/../deploy/nginx"
		into "$buildDir/dist/nginx"
	}

	copy {
		from "$buildDir/../deploy/prometheus/prometheus.yml"
		into "$buildDir/dist/prometheus/"
	}
}

build {
	finalizedBy(packageDistribution)
}

packageDistribution.dependsOn(copyArtifacts)
packageDistribution.dependsOn(adaptDockerCompose)
