plugins {
	id 'org.springframework.boot' version '4.0.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'idea'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jar {
	enabled = false
}

// GitHub-Version >> Docker Version >> Fallback
if (System.getenv("RELEASE_VERSION")) {
	version = System.getenv("RELEASE_VERSION")
}
else if (project.hasProperty("RELEASE_VERSION") && project.getProperty("RELEASE_VERSION") && !project.getProperty("RELEASE_VERSION").allWhitespace) {
	version = project.getProperty("RELEASE_VERSION")
}
else {
	version = "SNAPSHOT"
}
println("Building version $version")

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

springBoot {
	buildInfo()
}


tasks.register('packageDistribution', Zip) {
    archiveFileName = "heizung-monitoring.zip"
    destinationDirectory = layout.buildDirectory.dir("release")
    from(layout.buildDirectory.dir("dist"))
}

// Adapt docker-compose.yml with correct version
tasks.register('adaptDockerCompose', Copy) {
    from(layout.buildDirectory.dir("../deploy/docker-compose.yml"))
    into(layout.buildDirectory.dir("dist"))
    filter { line -> line.replaceAll('latest', version) }
}

tasks.register('copyArtifacts') {
    doLast {
        mkdir(layout.buildDirectory.dir("dist/grafana").get().asFile)

        copy {
            from(layout.buildDirectory.dir("../deploy/nginx"))
            into(layout.buildDirectory.dir("dist/nginx"))
        }

        copy {
            from(layout.buildDirectory.dir("../deploy/prometheus/prometheus.yml"))
            into(layout.buildDirectory.dir("dist/prometheus"))
        }
    }
}

build {
	finalizedBy(packageDistribution)
}

packageDistribution.dependsOn(copyArtifacts)
packageDistribution.dependsOn(adaptDockerCompose)
